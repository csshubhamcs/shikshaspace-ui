plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.6'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'com.vaadin' version '24.9.3'
    id 'com.diffplug.spotless' version '8.0.0'
}

group = 'com.shikshaspace'
version = '0.0.1-SNAPSHOT'
description = 'shikshaspace project'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

ext {
    set('vaadinVersion', "24.9.3")
}

dependencies {
    implementation 'com.vaadin:vaadin-spring-boot-starter'
    implementation 'org.springframework.boot:spring-boot-starter-webflux'

    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'

    implementation 'com.fasterxml.jackson.core:jackson-databind'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'

    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation 'io.projectreactor:reactor-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

dependencyManagement {
    imports {
        mavenBom "com.vaadin:vaadin-bom:${vaadinVersion}"
    }
}

tasks.named('test') {
    useJUnitPlatform()
}

tasks.register('buildProduction') {
    dependsOn 'vaadinBuildFrontend'
    dependsOn 'bootJar'
    description = 'Builds optimized production bundle'
    group = 'build'
}

vaadin {
    productionMode = true
}

spotless {
    java {
        googleJavaFormat('1.17.0')
        targetExclude 'frontend/generated/**/*.java', 'build/**/*.java', '**/generated/**/*.java'
        removeUnusedImports()
        trimTrailingWhitespace()
        endWithNewline()
    }
}

tasks.named('spotlessJava').configure {
    mustRunAfter tasks.named('vaadinPrepareFrontend')
}

tasks.named('spotlessCheck').configure {
    mustRunAfter tasks.named('vaadinPrepareFrontend')
}
